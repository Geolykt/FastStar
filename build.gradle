plugins {
    id 'java'
    id 'java-library'
    id 'gsl-starplane' version '0.2.0-a20240624.1'
    //id 'gsl-starplane' version '0.2.0'
    id 'maven-publish'
}

group 'de.geolykt'
version '2.0.0'

// The following statement defines that Java 8 should be used (which is galimulator's runtime version),
// however if you dare, you can bump the version - just beware that users may lack the knowledge in how to install and run
// your mod in that case.
targetCompatibility = compileJava.targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = '1.8'

repositories {
    mavenLocal()
    // stianloader's maven repository, which is the repo where SLAPI and SLL are contianed in (among other stianloader toolchain stuff)
    maven {
        name 'stianloader-maven'
        url 'https://stianloader.org/maven'
    }
    mavenCentral()
}

starplane {
    mappingsFile("tinyv2", "newStarmap.tinyv2")
}

configurations {
    dependencyModsRuntime
    compileOnlyApi.extendsFrom(devRuntime)
    compileOnlyApi.extendsFrom(galimulatorDependencies)
}

deployMods {
    from configurations["dependencyModsRuntime"]
    remapMods = true
}

dependencies {
    // The versions of SLAPI are listed here: https://stianloader.org/maven/de/geolykt/starloader-api/
    compileOnlyApi "de.geolykt:starloader-api:2.0.0:remapped"
    // The versions of launcher-micromixin are listed here: https://stianloader.org/maven/org/stianloader/launcher-micromixin/
    devRuntime "org.stianloader:launcher-micromixin:4.0.0"
    compileOnly "org.stianloader:micromixin-annotations:0.5.0-a20240518"
    compileOnly "de.geolykt.starloader:starplane-annotations:1.0.0"
    compileOnlyApi "org.jetbrains:annotations:24.1.0"


    // Library mods
    dependencyModsRuntime "de.geolykt:starloader-api:2.0.0-a20240624.1:remapped"

    // Note: these dependencies have to be built yourself as they aren't published on a maven repo (yet)
    // However, they are completely optional - even for the build process so feel free to comment these lines out

    // Specialised (lossful) performance mods
    dependencyModsRuntime "de.geolykt:fast-asynchronous-starlane-triangulator:1.1.0"
    dependencyModsRuntime "de.geolykt:fast-galaxy-generation:0.1.0:remapped"

    // Profiler
    dependencyModsRuntime "de.geolykt:galimulator-profiler:0.0.1-SNAPSHOT"

    // "Other" mods
    dependencyModsRuntime "de.geolykt:star-cell-shading:0.2.2:remapped"
}

remapJar {
    archiveClassifier = 'remapped'
    fromJar jar
    dependsOn jar
}

runMods {
    from jar
    systemProperties.put("org.stianloader.sll.log.MIRROR_MAVEN_REQUESTS", true)
    systemProperties.put("org.stianloader.sll.log.level", "DEBUG")
    systemProperties.put("classloader.dump", true)
}

build {
    dependsOn remapJar
}

publishing {
    publications {
        plugin(MavenPublication) { publication ->
            groupId project.group
            artifactId project.base.archivesName.get()

            from components['java']
            artifact remapJar
        }
    }
    repositories {
        if (System.getProperty('publishRepo') != null) {
            maven {
                url System.getProperty('publishRepo')
                allowInsecureProtocol = true
            }
        } else {
            mavenLocal()
        }
    }
}

genEclipseRuns {
    additionalRuntimeDependency("main", configurations["runtimeClasspath"])
}
